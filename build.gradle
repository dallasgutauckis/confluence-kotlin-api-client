buildscript {
   ext.kotlinVersion = '1.2.51'

   repositories {
       mavenCentral()
   }
   dependencies {
       classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
   }
}

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.dallasgutauckis.confluence'
version '1.0.1-SNAPSHOT'

ext {
    okhttpVersion = '3.11.0'
    retrofitVersion = '2.4.0'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    compile 'com.google.code.gson:gson:2.8.5'
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-java8:${retrofitVersion}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Confluence Kotlin API Client'
                packaging 'jar'
                description 'A client for the Confluence REST API'
                url 'https://github.com/dallasgutauckis/confluence-kotlin-api-client'

                scm {
                    connection 'git@github.com:dallasgutauckis/confluence-kotlin-api-client.git'
                    developerConnection 'git@github.com:dallasgutauckis/confluence-kotlin-api-client.git'
                    url 'https://github.com/dallasgutauckis/confluence-kotlin-api-client'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'dallas'
                        name 'Dallas Gutauckis'
                        email 'dallas@gutauckis.com'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}
